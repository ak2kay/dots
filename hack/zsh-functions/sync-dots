# This is a function because it needs to access HISTFILE.

emulate -L zsh -o no_unset -o no_prompt_subst -o prompt_percent -o pushd_silent

local GIT_DIR GIT_WORK_TREE
unset GIT_DIR GIT_WORK_TREE

local merge=1 OPTIND OPTARG
while getopts ":hm" opt; do
  case $opt in
    *h)
      print -r -- $'Usage: sync-dots [{+|-}m]\nSynchronize local dots with GitHub.'
      return 0
    ;;
    \?) print -r -- "sync-dots: invalid option: $OPTARG" >&2;            return 1;;
    :)  print -r -- "sync-dots: missing required argument: $OPTARG" >&2; return 1;;
    m)  merge=0;;
    +m) merge=1;;
  esac
done

if (( OPTIND <= ARGC )); then
  print -r -- "sync-dots: unexpected positional argument: ${*[OPTIND]}" >&2
  return 1
fi

function -sync-dots-repo() {
  local repo=${1} dirty=0 s
  s="$(git status --porcelain --untracked-files=no --ignore-submodules=dirty)" || return
  if [[ -n $s ]]; then
    dirty=1
    git stash || return
  fi

  print -Pr -- "%F{yellow}sync-dots%f: pulling %B$repo%b" >&2
  if ! git pull --rebase --no-recurse-submodules && ! git pull --no-edit --no-recurse-submodules; then
    print -Pr -- "%F{red}sync-dots%f: failed to pull %B$repo%b" >&2
    git status || return
    return 1
  fi

  if (( merge )) && git remote get-url upstream &>/dev/null; then
    print -Pr -- "%F{yellow}sync-dots%f: merging upstream %B$repo%b" >&2
    git fetch upstream || return
    if ! git merge --no-edit upstream/master; then
      print -Pr -- "%F{red}sync-dots%f: failed to merge upstream %B$repo%b" >&2
      git status || return
      return 1
    fi
  fi

  print -Pr -- "%F{yellow}sync-dots%f: pushing %B$repo%b" >&2
  git push || return
  if (( dirty )); then
    git stash pop || return
  fi

  print -Pr -- "%F{yellow}sync-dots%f: pulling submodules from %B$repo%b" >&2
  git pull --recurse-submodules || return
  git submodule update --init || return
}

{
  pushd -q ~ || return

  pushd -q ~/.dots || return
  -sync-dots-repo dots || return
  popd -q

  pushd -q ~/.dots-private || return
  local hist=${ZDOTDIR:-~}/.zsh_history.${(%):-%m}
  local -U hist=($hist{,:*}(N))
  [[ -f ${HISTFILE:-} ]] && hist+=($HISTFILE)
  if (( $#hist )); then
    cp $hist .
    git add .zsh_history* || return
    local s
    s="$(git status --porcelain .zsh_history*)" || return
    if [[ -n $s ]]; then
      git commit -m 'chore(history): fresh history' .zsh_history* || return
    fi
  fi
  -sync-dots-repo dots-private || return
  popd -q
} always {
  unset -f -- -sync-dots-repo
  popd -q
}
